enableFeaturePreview('VERSION_CATALOGS')
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }
    versionCatalogs {
        deps {
            // versions
            version('ktx', '1.3.0')
            version('kotlin', '1.5.31')
            version('groovy', '3.0.5')
            version('checkstyle', '8.37')
            // libraries
            alias('core-ktx').to('androidx.core', 'core-ktx').versionRef('ktx')
            alias('kotlin-gradle-plugin').to('org.jetbrains.kotlin', 'kotlin-gradle-plugin').versionRef('kotlin')
            alias('groovy-core').to('org.codehaus.groovy', 'groovy').versionRef('groovy')
            alias('groovy-json').to('org.codehaus.groovy', 'groovy-json').versionRef('groovy')
            alias('groovy-nio').to('org.codehaus.groovy', 'groovy-nio').versionRef('groovy')
            alias('commons-lang3').to('org.apache.commons', 'commons-lang3').version {
                strictly '[3.8, 4.0['
                prefer '3.9'
            }
            // bundles
            bundle('groovy', ['groovy-core', 'groovy-json', 'groovy-nio'])
            // plugins
            alias('jmh').toPluginId('me.champeau.jmh').version('0.6.5')
        }
    }
}
rootProject.name = "bytecode"
include ':app'
